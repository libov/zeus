      DOUBLE PRECISION FUNCTION SIG1Q(XX,VWGT)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION XX(10)
      DIMENSION DVEC(10),BET1(3),BET2(3)
      DIMENSION P1(0:3),P2(0:3),P3(0:3),Q(0:3)
      DIMENSION PPRO(0:3),PIEL(0:3),PSEL(0:3),PGAM(0:3)

      COMMON/BEAM/EPRO,EIEL
      COMMON/INVAR/XBJ,XIMIN,XM2,Q2
      COMMON/KINE/S,SP,BETA
      COMMON/FACTORS/EH2,CA,CF,XKGP,XKQP,N
      COMMON/CHARGE/EH,EU,ED,ES,EC,EB,ET
      COMMON/PDFQ /FU ,FD ,FS ,FC ,FB
      COMMON/PDFAQ/FUB,FDB,FSB,FCB,FBB
      COMMON/QUARKF/IQUARK
      COMMON/FRACTION/X0,Y0
      COMMON/AZDEP/CTH22,STH12
      COMMON/FLAGS/ISCALER,ISCALEF,IALPHAS,IFRAG
      COMMON/LIMITS/Q2MIN,Q2MAX,XMIN,XMAX,YMIN,YMAX
      COMMON/FRAGP/EPS,HFRAC,XMD,ISL
      COMMON/MOMFRAC/XI

C Adopted from Massimo
      INTEGER IFL
      COMMON /DOFL/ IFL

      PARAMETER (TINY=1.D-5)
      PARAMETER (PI=3.141592653589793D0)
      PARAMETER (PI3=PI*PI*PI)
      PARAMETER (PIB2=0.5D0*PI)
      PARAMETER (XME2=2.6D-7)
      PARAMETER (HBARC2=0.3894D6)
      PARAMETER (DTEST=1D-7)

C*TC>
*
      DOUBLE PRECISION MYXI,MYXMUF2
      COMMON/MYPDF/MYXI,MYXMUF2
C*TC<

      DATA NCOUNT/0/
      SAVE NCOUNT

C. SET IFAC2=0 AND IFACL=1 FOR L COMPONENT ONLY
C. SET IFAC2=1 AND IFACL=0 FOR 2 COMPONENT ONLY
      IFAC2=1
      IFACL=1
C Adopted from Massimo
      IF (IFL.EQ.0) IFACL=0
      IF (IFL.EQ.2) IFAC2=0
C. Y
      SS=4D0*EPRO*EIEL
      YU=MIN(YMAX,1D0)
      YL=MAX(YMIN,4*XM2/SS)
      XJACYBJ=YU-YL
      YBJ=YL+XJACYBJ*XX(1)
      OMYBJ=1D0-YBJ
C. Q2
      Q2U=MIN(YBJ*SS-4*XM2,Q2MAX)
      Q2L=MAX(XME2*YBJ*YBJ/OMYBJ,Q2MIN)
      XJACQ2=Q2U-Q2L
      Q2=Q2L+XJACQ2*XX(2)
C. CALCULATE XBJ AND CUT ON IT 
      XBJ=Q2/(YBJ*SS)
      IF( (XBJ.LE.MAX(0D0,XMIN)).OR.(XBJ.GE.MIN(1D0,XMAX) ))GOTO 1000
C. APPLY MAPPING ON XI
      XIMIN = XBJ*(4D0*XM2+Q2)/Q2
      IF(XIMIN.GE.1D0)GOTO 1000
      CALL BRM48(DVEC,1)
      CALL MAP(2D0,DVEC(1),XIMIN,1D0,XI,XJACXI)
C. CALCULATE ENERGIES
      SH = Q2*(1D0-XBJ)/XBJ
      SPH = SH + Q2
      SP = XI*SPH
      S = SP - Q2
      SP3=SP*SP*SP
C. CHECK IF ABOVE THRESHOLD
      RHO = 4D0*XM2/S
      IF(RHO.GE.1D0)GOTO 1000
      BETA = DSQRT(1D0-RHO)

      RHOX = (4D0*XM2+Q2)/SP
      ONEMRX = 1D0-RHOX

      TINY2 = XI*TINY/XBJ
      X = TINY2+(1D0-TINY2)*XX(3)*XX(3)
      XJACX = 2D0*XX(3)
      X = 1D0-X*ONEMRX
      XJACX = XJACX*ONEMRX

      OMEGA = 2D0*Y0
      RHOTILDE = 1D0 - X0 * ONEMRX

      ONEMX = 1D0 - X

      XJACY = 2D0
      Y = XJACY*XX(4)-1D0
      TTT=TINY+(1D0-TINY)*Y*Y
      XJACY=XJACY*2D0*DABS(Y)
      IF(Y.GT.0D0)THEN
        TH=TTT*PIB2
      ELSE
        TH=PI-TTT*PIB2
      ENDIF
      Y=DCOS(TH)
      XJACY=XJACY*DSIN(TH)*PIB2
      ONEPY = 1D0 + Y
      ONEMY = 1D0 - Y

      XJAC1 = PI
      THETA1 = XJAC1*XX(5)
      CTH1 = DCOS(THETA1)
      STH1 = DSIN(THETA1)
      STH12 = STH1*STH1

      XJAC2 = PI
      THETA2 = XJAC2*XX(6)
      CTH2 = DCOS(THETA2)
      CTH22 = CTH2*CTH2
C. JACOBIAN BETWEEN {R1,...,R7} <-> {Q2,YBJ,X,Y,XI,TH1,TH2}
      XJAC = XJACQ2*XJACYBJ*XJACX*XJACY*XJACXI*XJAC1*XJAC2
C. SETUP BOOST TO PROTON-GAMMA* CMS
      BET1(1)=0D0
      BET1(2)=0D0
      BET1(3)=(XI-1D0)/(1D0+XI-2D0*XBJ)
C. SETUP BOOST TO LAB FRAME
C. FIRST CONSTRUCT P(PROTON) AND Q(PHOTON) IN LAB FRAME
C. H1 AND ZEUS TAKE PROTON IN +Z DIRECTION
      PPRO(0)=EPRO
      PPRO(1)=0D0
      PPRO(2)=0D0
      PPRO(3)=EPRO
C. AND INCIDENT ELECTRON IN -Z DIRECTION
      PIEL(0)=EIEL
      PIEL(1)=0D0
      PIEL(2)=0D0
      PIEL(3)=-EIEL
C. THE SCATTERED ELECTRON ENERGY AND ANGLE ARE GIVEN 
C. IN TERMS OF Q2,Y AND ENERGY OF INCIDENT ELECTRON
      ESEL  =(Q2+4D0*EIEL*EIEL*OMYBJ)/(4D0*EIEL)
      CTHSEL=(Q2-4D0*EIEL*EIEL*OMYBJ)/(Q2+4D0*EIEL*EIEL*OMYBJ)
      STHSEL=DSQRT(1D0-CTHSEL*CTHSEL)
      PSEL(0)=ESEL
      PSEL(1)=0D0
      PSEL(2)=ESEL*STHSEL
      PSEL(3)=ESEL*CTHSEL
C. NOW TO FIND PHOTON FOUR VECTOR
      DO 10 I=0,3
 10   PGAM(I)=PIEL(I)-PSEL(I)
C. SET UP BOOST TO LAB FRAME
      DO 15 I=1,3
 15   BET2(I)=(PPRO(I)+PGAM(I))/(PPRO(0)+PGAM(0))
C. FIND BOOST DEPENDENT ROTATION ANGLE
      CALL RANGLE(PGAM,BET2,COSA,SINA)
C. CALL STRUCTURE FUNCTIONS

      CALL MANDEL3(
     &  X,Y,CTH1,THETA2,
     &  T1,U1,TP,UP,S3,S4,S5,U6,U7,BETA5,DA,DB,
     &  P1,P2,P3)

C. SET AUX SCALE TO W2
C*TC>
C      XMX2=max(P1(1)**2+P1(2)**2,P2(1)**2+P2(2)**2)
C      XMX2=P1(1)**2+P1(2)**2
      XMX2=(P1(1)**2+P1(2)**2+P2(1)**2+P2(2)**2)/2.
C*TC<
* 
C. SET RENORMALIZATION SCALE
      CALL MSCALE(ISCALER,Q2,XM2,XMX2,XMUR2)
C. SET MASS FACTORIZATION SCALE
      CALL MSCALE(ISCALEF,Q2,XM2,XMX2,XMUF2)
C. CALCULATE STRONG COUPLING
      ALFAS=ALPHAS(IALPHAS,XMUR2,NEFF)
      B0=(33D0-2D0*NEFF)/3D0
C. CALCULATE EM COUPLING
      AEM2=HWUAEM(XMUR2)**2
C. FILL PDFS
C*TC>
      MYXI=XI
      MYXMUF2=XMUF2
C*TC<
      CALL PDF(XI,XMUF2)
C. OVERALL FACTOR
      XNORM = STH1*ALFAS*ALFAS*Q2*N*CF*XKQP/(256D0*PI3*XI)
C.
      IF ( NCOUNT.EQ.0 ) THEN
        WRITE(*,*) 'ALFAS  = ',ALFAS
        WRITE(*,*) 'MUR2   = ',XMUR2
        WRITE(*,*) 'LOGMUR = ',DLOG(XMUR2/XM2)
        WRITE(*,*) 'MUF2   = ',XMUF2
        WRITE(*,*) 'LOGMUF = ',DLOG(XMUF2/XM2)
        WRITE(*,*) ' '
        WRITE(*,*) 'INTEGRATING QUARK INITIATED ...'
        WRITE(*,*) ' '
      END IF

C      Q(0)=(S-Q2)/(2D0*DSQRT(S))
C      Q(1)=0D0
C      Q(2)=0D0
C      Q(3)=-(S+Q2)/(2D0*DSQRT(S))
C      CALL BOOST(-1,BET1,Q)
C      CALL ROT(+1,Q,COSA,SINA)
C      CALL BOOST(-1,BET2,Q)
C      DO 20 I=0,3
C        IF(DABS(Q(I)-PGAM(I)).GT.DTEST)THEN
C           WRITE(*,*) 'TROUBLE IN ROTATION AND BOOST'
C           WRITE(*,*) I,DABS(Q(I)-PGAM(I))
C        ENDIF
C 20   CONTINUE
C. RANDOMIZE ABOUT Z AXIS
      CALL BRM48(DVEC,1)
      PHI=2D0*PI*DVEC(1)
      COSP=DCOS(PHI)
      SINP=DSIN(PHI)

      TMP1= P1(1)*COSP+P1(2)*SINP
      TMP2=-P1(1)*SINP+P1(2)*COSP
      P1(1)=TMP1
      P1(2)=TMP2
      TMP1= P2(1)*COSP+P2(2)*SINP
      TMP2=-P2(1)*SINP+P2(2)*COSP
      P2(1)=TMP1
      P2(2)=TMP2
      TMP1= P3(1)*COSP+P3(2)*SINP
      TMP2=-P3(1)*SINP+P3(2)*COSP
      P3(1)=TMP1
      P3(2)=TMP2

C      write(6,*) ' P1= ',p1
C      write(6,*) ' P2= ',p2
C      write(6,*) ' P3= ',p3

      CALL BOOST(-1,BET1,P1)
      CALL ROT(+1,P1,COSA,SINA)
      CALL BOOST(-1,BET2,P1)
      CALL BOOST(-1,BET1,P2)
      CALL ROT(+1,P2,COSA,SINA)
      CALL BOOST(-1,BET2,P2)
      CALL BOOST(-1,BET1,P3)
      CALL ROT(+1,P3,COSA,SINA)
      CALL BOOST(-1,BET2,P3)

C. PETERSON FRAGMENTATION
      IF (IFRAG.EQ.1) THEN
         IF(ISL.EQ.0)THEN
            CALL BRM48(DVEC,2)
            CALL FRAG(DVEC,P1,P2)
         ELSEIF(ISL.EQ.1)THEN
            CALL BRM48(DVEC,10)
            CALL FRAG(DVEC,P1,P2)
         ELSE
            WRITE(*,*) 'UNKNOWN ISL: ',ISL
            STOP
         ENDIF
      ENDIF
      
C
C ========================== D =====================================
C

      Q2DG = FQGD(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      Q2DG = Q2DG*BETA5*SP*ONEMX/(S*TP)
      Q2DL = FQLD(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      Q2DL = Q2DL*BETA5*SP*ONEMX/(S*TP)
      IF ( IQUARK.EQ.1 ) THEN
         Q2DG = Q2DG*( EU*EU*FU + ED*ED*FD + ES*ES*FS )
         Q2DL = Q2DL*( EU*EU*FU + ED*ED*FD + ES*ES*FS )
      ELSE IF ( IQUARK.EQ.2 ) THEN
         Q2DG = Q2DG*( EU*EU*FU + ED*ED*FD + ES*ES*FS + EC*EC*FC )
         Q2DL = Q2DL*( EU*EU*FU + ED*ED*FD + ES*ES*FS + EC*EC*FC )
      ELSE
         WRITE(*,*) 'ERROR IN IQUARK: ',IQUARK
         STOP
      END IF

      AQ2DG = FAQGD(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      AQ2DG = AQ2DG*BETA5*SP*ONEMX/(S*TP)
      AQ2DL = FAQLD(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      AQ2DL = AQ2DL*BETA5*SP*ONEMX/(S*TP)
      IF ( IQUARK.EQ.1 ) THEN
         AQ2DG = AQ2DG*( EU*EU*FUB + ED*ED*FDB + ES*ES*FSB )
         AQ2DL = AQ2DL*( EU*EU*FUB + ED*ED*FDB + ES*ES*FSB )
      ELSE IF ( IQUARK.EQ.2 ) THEN
         AQ2DG = AQ2DG*( EU*EU*FUB + ED*ED*FDB + ES*ES*FSB + EC*EC*FCB )
         AQ2DL = AQ2DL*( EU*EU*FUB + ED*ED*FDB + ES*ES*FSB + EC*EC*FCB )
      ELSE
         WRITE(*,*) 'ERROR IN IQUARK: ',IQUARK
         STOP
      END IF

C
C ========================== 0 =====================================
C

      Q20G = FQG0(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      Q20G = Q20G*BETA5*SP*ONEMX/(S*TP)
      Q20L = FQL0(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      Q20L = Q20L*BETA5*SP*ONEMX/(S*TP)
      IF ( IQUARK.EQ.1 ) THEN
         Q20G = Q20G*EH*( EU*FU + ED*FD + ES*FS )
         Q20L = Q20L*EH*( EU*FU + ED*FD + ES*FS )
      ELSE IF ( IQUARK.EQ.2 ) THEN
         Q20G = Q20G*EH*( EU*FU + ED*FD + ES*FS + EC*FC )
         Q20L = Q20L*EH*( EU*FU + ED*FD + ES*FS + EC*FC )
      ELSE
         WRITE(*,*) 'ERROR IN IQUARK: ',IQUARK
         STOP
      END IF

      AQ20G = FAQG0(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      AQ20G = AQ20G*BETA5*SP*ONEMX/(S*TP)
      AQ20L = FAQL0(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      AQ20L = AQ20L*BETA5*SP*ONEMX/(S*TP)
      IF ( IQUARK.EQ.1 ) THEN
         AQ20G = AQ20G*EH*( - EU*FUB - ED*FDB - ES*FSB )
         AQ20L = AQ20L*EH*( - EU*FUB - ED*FDB - ES*FSB )
      ELSE IF ( IQUARK.EQ.2 ) THEN
         AQ20G = AQ20G*EH*( - EU*FUB - ED*FDB - ES*FSB - EC*FCB )
         AQ20L = AQ20L*EH*( - EU*FUB - ED*FDB - ES*FSB - EC*FCB )
      ELSE
         WRITE(*,*) 'ERROR IN IQUARK: ',IQUARK
         STOP
      END IF

C
C ========================== C =====================================
C

C FIRST COMES THE "EVENT"

C THIS IS +F(Y)/(1+Y)

      EVENTG1 = FQGC(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      EVENTG1 = -2D0*EVENTG1*BETA5/(SP*ONEPY)
      EVENTL1 = FQLC(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      EVENTL1 = -2D0*EVENTL1*BETA5/(SP*ONEPY)
      IF ( IQUARK.EQ.1 ) THEN
         EVENTG1 = EVENTG1*EH2*( FU + FD + FS )
         EVENTL1 = EVENTL1*EH2*( FU + FD + FS )
      ELSE IF ( IQUARK.EQ.2 ) THEN
         EVENTG1 = EVENTG1*EH2*( FU + FD + FS + FC )
         EVENTL1 = EVENTL1*EH2*( FU + FD + FS + FC )
      ELSE
         WRITE(*,*) 'ERROR IN IQUARK: ',IQUARK
         STOP
      END IF

      EVENTG2 = FAQGC(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      EVENTG2 = -2D0*EVENTG2*BETA5/(SP*ONEPY)
      EVENTL2 = FAQLC(X,Y,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
      EVENTL2 = -2D0*EVENTL2*BETA5/(SP*ONEPY)
      IF ( IQUARK.EQ.1 ) THEN
         EVENTG2 = EVENTG2*EH2*( FUB + FDB + FSB )
         EVENTL2 = EVENTL2*EH2*( FUB + FDB + FSB )
      ELSE IF ( IQUARK.EQ.2 ) THEN
         EVENTG2 = EVENTG2*EH2*( FUB + FDB + FSB + FCB )
         EVENTL2 = EVENTL2*EH2*( FUB + FDB + FSB + FCB )
      ELSE
         WRITE(*,*) 'ERROR IN IQUARK: ',IQUARK
         STOP
      END IF

C PLOT EVENT

      WGTG = (Q2DG+AQ2DG+Q20G+AQ20G+EVENTG1+EVENTG2)*XNORM*XJAC
      WGTL = (Q2DL+AQ2DL+Q20L+AQ20L+EVENTL1+EVENTL2)*XNORM*XJAC
      WGT2 = WGTG+3D0*WGTL
      WGTL = 2D0*WGTL
      WGT = (1D0+OMYBJ*OMYBJ)*WGT2*IFAC2-YBJ*YBJ*WGTL*IFACL
      WGT = 2D0*PI*AEM2*HBARC2*WGT/(YBJ*Q2*Q2)
      WGT = WGT*VWGT
C*TC>
C      WGT=1.e-06
C*TC<
      CALL FHIST(WGT,P1,P2,P3)
      HWGT = WGT/VWGT

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C THEN COMES THE "COUNTER-EVENT" C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

C COLLINEAR PART

      IF ( ONEPY.LT.OMEGA ) THEN

        YTMP = -1D0

        CALL MANDEL3(
     &    X,YTMP,CTH1,THETA2,
     &    T1,U1,TP,UP,S3,S4,S5,U6,U7,BETA5,DA,DB,
     &    P1,P2,P3)

C. RANDOMIZE ABOUT Z AXIS
        TMP1= P1(1)*COSP+P1(2)*SINP
        TMP2=-P1(1)*SINP+P1(2)*COSP
        P1(1)=TMP1
        P1(2)=TMP2
        TMP1= P2(1)*COSP+P2(2)*SINP
        TMP2=-P2(1)*SINP+P2(2)*COSP
        P2(1)=TMP1
        P2(2)=TMP2
        TMP1= P3(1)*COSP+P3(2)*SINP
        TMP2=-P3(1)*SINP+P3(2)*COSP
        P3(1)=TMP1
        P3(2)=TMP2

        CALL BOOST(-1,BET1,P1)
        CALL ROT(+1,P1,COSA,SINA)
        CALL BOOST(-1,BET2,P1)
        CALL BOOST(-1,BET1,P2)
        CALL ROT(+1,P2,COSA,SINA)
        CALL BOOST(-1,BET2,P2)
        CALL BOOST(-1,BET1,P3)
        CALL ROT(+1,P3,COSA,SINA)
        CALL BOOST(-1,BET2,P3)

        IF (IFRAG.EQ.1) THEN
           CALL FRAG(DVEC,P1,P2)
        ENDIF

C THIS IS -F(-1)/(1+Y)

        CEVENTG11 =
     &       FQGC(X,YTMP,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
        CEVENTG11 = 2D0*CEVENTG11*BETA5/(SP*ONEPY)

        CEVENTL11 =
     &       FQLC(X,YTMP,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
        CEVENTL11 = 2D0*CEVENTL11*BETA5/(SP*ONEPY)

        CEVENTG12 =
     &       FAQGC(X,YTMP,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
        CEVENTG12 = 2D0*CEVENTG12*BETA5/(SP*ONEPY)

        CEVENTL12 =
     &       FAQLC(X,YTMP,XM2,Q2,S,SP,T1,U1,TP,UP,S3,S4,S5,U6,U7,DP,DM)
        CEVENTL12 = 2D0*CEVENTL12*BETA5/(SP*ONEPY)

C THEN THE COLLINEAR CONTRIBUTION

        FX=1D0+(1D0+ONEMX*ONEMX)/(X*X)*
     &(DLOG(SP/XMUF2)+DLOG(SP/S)+2D0*DLOG(ONEMX)+DLOG(0.5D0*OMEGA))

        CEVENTG21 = 16D0*FX*BGQED(X*T1,X*SP,XM2,Q2)
        CEVENTG21 = CEVENTG21*BETA5/(SP*OMEGA)

        CEVENTL21 = 16D0*FX*BLQED(X*T1,X*SP,XM2,Q2)
        CEVENTL21 = CEVENTL21*BETA5/(SP*OMEGA)

        CEVENTG22 = CEVENTG21

        CEVENTL22 = CEVENTL21

C SUM THE PIECES OF THE COUNTER EVENT

        CEVENTG1 = ( CEVENTG11 + CEVENTG21 )
        CEVENTL1 = ( CEVENTL11 + CEVENTL21 )
        IF ( IQUARK.EQ.1 ) THEN
          CEVENTG1 = CEVENTG1*EH2*( FU + FD + FS )
          CEVENTL1 = CEVENTL1*EH2*( FU + FD + FS )
        ELSE IF ( IQUARK.EQ.2 ) THEN
          CEVENTG1 = CEVENTG1*EH2*( FU + FD + FS + FC )
          CEVENTL1 = CEVENTL1*EH2*( FU + FD + FS + FC )
        ELSE
          WRITE(*,*) 'ERROR IN IQUARK: ',IQUARK
          STOP
        END IF

        CEVENTG2 = ( CEVENTG12 + CEVENTG22 )
        CEVENTL2 = ( CEVENTL12 + CEVENTL22 )
        IF ( IQUARK.EQ.1 ) THEN
          CEVENTG2 = CEVENTG2*EH2*( FUB + FDB + FSB )
          CEVENTL2 = CEVENTL2*EH2*( FUB + FDB + FSB )
        ELSE IF ( IQUARK.EQ.2 ) THEN
          CEVENTG2 = CEVENTG2*EH2*( FUB + FDB + FSB + FCB )
          CEVENTL2 = CEVENTL2*EH2*( FUB + FDB + FSB + FCB )
        ELSE
          WRITE(*,*) 'ERROR IN IQUARK: ',IQUARK
          STOP
        END IF

C PLOT COUNTER EVENT

        WGTG = ( CEVENTG1 + CEVENTG2 )*XNORM*XJAC
        WGTL = ( CEVENTL1 + CEVENTL2 )*XNORM*XJAC
        WGT2 = WGTG+3D0*WGTL
        WGTL = 2D0*WGTL
        WGT = (1D0+OMYBJ*OMYBJ)*WGT2*IFAC2-YBJ*YBJ*WGTL*IFACL
        WGT = 2D0*PI*AEM2*HBARC2*WGT/(YBJ*Q2*Q2)
        WGT = WGT*VWGT
C*TC>
C        WGT=1.e-09
C*TC<
        CALL FHIST(WGT,P1,P2,P3)
        CWGT = WGT/VWGT

       ELSE

        CWGT = 0D0

       END IF 

C SUM ALL CONTRIBUTIONS

      SIG1Q=HWGT+CWGT
*
      NCOUNT = 1
      RETURN
C. REJECTED EVENTS GET WEIGHT ZERO
 1000 CONTINUE
      SIG1Q=0D0
      RETURN
      END
